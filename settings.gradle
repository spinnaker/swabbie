/*
 * Copyright 2017 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

['kork'].each { prj ->
  String propName = "${prj}Composite"
  String projectPath = "../$prj"
  if (settings.ext.has(propName) && Boolean.parseBoolean(settings.ext.get(propName) as String)) {
    includeBuild projectPath
  }
}

enableFeaturePreview("VERSION_ORDERING_V2")

rootProject.name = 'swabbie'

include 'swabbie-test',
  'swabbie-core',
  'swabbie-retrofit',
  'swabbie-redis',
  'swabbie-aws',
  'swabbie-echo',
  'swabbie-orca',
  'swabbie-clouddriver',
  'swabbie-front50',
  'swabbie-web',
  'swabbie-bom'

def setBuildFile(project) {
  project.buildFileName = "${project.name}.gradle"
  project.children.each {
    setBuildFile(it)
  }
}

rootProject.children.each {
  setBuildFile it
}
